plugins {
    id "local.versions"

    id 'java-library'
    id 'maven-publish'
    alias(libs.plugins.com.diffplug.spotless)
    alias(libs.plugins.org.asciidoctor.jvm.convert) apply false
}

group = "org.hibernate.reactive"
// leverage the ProjectVersion which comes from the `local.versions` plugin
version = project.projectVersion.fullName

rootProject.afterEvaluate {
    // Workaround since "libs.versions.NAME" notation cannot be used here
    def libs = project.extensions.getByType(VersionCatalogsExtension).named('libs')
    logger.lifecycle "ORM version: ${libs.findVersion('hibernateOrmVersion').get().requiredVersion}"
    logger.lifecycle "ORM Gradle plugin version: ${libs.findVersion('hibernateOrmGradlePluginVersion').get().requiredVersion}"
    logger.lifecycle "Vert.x SQL Client version: ${libs.findVersion('vertxSqlClientVersion').get().requiredVersion}"
}

tasks.register('copyDependenciesSonar', Copy) {
    description = "Aggregates all runtime dependencies for Sonar CLI analysis."

    def targetProjects = subprojects.findAll { it.name != 'reporting' }

    targetProjects.each { sub ->
        evaluationDependsOn(sub.path)

        if (sub.plugins.hasPlugin('java')) {
            from(sub.configurations.runtimeClasspath)
        }
    }

    into(layout.buildDirectory.dir("sonar-dependencies"))

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
