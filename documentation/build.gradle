import java.time.Year

import org.asciidoctor.gradle.jvm.AsciidoctorTask
import org.ysb33r.grolifant.api.core.jvm.ExecutionMode

plugins {
	id "org.asciidoctor.jvm.convert"
	id "hr-java-library"
}

configurations {
	themezip
}

dependencies {
	themezip 'org.hibernate.infra:hibernate-asciidoctor-theme:6.0.4.Final@zip'
}

ext {
	projectsToSkipWhenAggregatingJavadocs = [
			'example',
			'release',
			'documentation'
	]
}

rootProject.subprojects { subproject ->
	if ( !this.projectsToSkipWhenAggregatingJavadocs.contains( subproject.name ) ) {
		this.evaluationDependsOn( subproject.path )
	}
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Prepare the theme for Javadocs/Asciidoc
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
tasks.register('unpackTheme', Copy) {
	def unpackDir = rootProject.layout.buildDirectory.dir("unpacked-theme")

	onlyIf {
		!unpackDir.get().asFile.exists()
	}
	destinationDir = unpackDir.get().asFile

	def zipFile = configurations.themezip.singleFile
	from zipTree(zipFile)

	dependsOn configurations.themezip
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Aggregated JavaDoc
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

final File javadocDir = mkdir( project.layout.buildDirectory.dir( "javadocs" ) )

/**
 * Builds the JavaDocs aggregated (unified) across all the sub-projects
 */
def aggregateJavadocsTask = tasks.register( 'aggregateJavadocs', Javadoc ) {
	description = 'Builds the aggregated (unified) JavaDocs across all sub-projects'

	final int inceptionYear = 2020
	final int currentYear = Year.now().getValue()

	// exclude any generated sources and internal packages
	exclude( '**/generated-src/**' )
	exclude( '**/src/main/generated/**' )
	exclude( '**/internal/**' )
	exclude( '**/impl/**' )

	// apply standard config
	maxMemory = '512m'
	destinationDir = javadocDir
	configure( options ) {
		overview = project.file( 'src/main/javadoc/overview.html' )
		windowTitle = 'Hibernate Reactive API documentation'
		docTitle = "Hibernate Reactive API documentation ($project.version)"
		bottom = "Copyright &copy; $inceptionYear-$currentYear <a href=\"http://redhat.com\">Red Hat, Inc</a>. All Rights Reserved."
		// Pick the styles for the JDK that is used to "build" the Javadocs:
		stylesheetFile = rootProject.layout.buildDirectory.dir("unpacked-theme").get()
				.dir("hibernate-asciidoctor-theme").dir("javadoc").dir("jdk21").file("stylesheet.css").asFile
		use = true
		options.encoding = 'UTF-8'

		def matcher = libs.versions.hibernateOrmVersion =~ /\d+\.\d+/
		def ormMinorVersion = matcher.find() ? matcher.group() : "5.6"

		links = [
				'https://docs.oracle.com/en/java/javase/17/docs/api/',
				'https://jakarta.ee/specifications/bean-validation/3.1/apidocs/',
				'https://jakarta.ee/specifications/platform/11/apidocs/',
				"https://docs.hibernate.org/orm/" + ormMinorVersion + "/javadocs/"
		]

		options.addStringOption( 'Xdoclint:none', '-quiet' )

		if ( gradle.ext.javaToolchainEnabled ) {
			options.setJFlags( getProperty( 'toolchain.javadoc.jvmargs' ).toString().
					split( ' ' ).toList().findAll( { !it.isEmpty() } ) )
		}
	}

	if ( gradle.ext.javaToolchainEnabled ) {
		// Display version of Java tools
		doFirst {
			logger.lifecycle "Aggregating javadoc with '${javadocTool.get().metadata.installationPath}'"
		}
	}

	// process each project, building up:
	//      1) appropriate sources
	//      2) classpath
	parent.subprojects.each { Project subProject ->
		// skip certain sub-projects
		if ( !project.projectsToSkipWhenAggregatingJavadocs.contains( subProject.name ) ) {
			// we only care about the main SourceSet...
			source subProject.sourceSets.main.java

			classpath += subProject.sourceSets.main.output + subProject.sourceSets.main.compileClasspath
		}
	}

	dependsOn 'unpackTheme'
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// Asciidoc
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

asciidoctor {
	// we do not want it creating its "default task"
	enabled = false
}

def renderReferenceDocumentationTask = tasks.register( "renderReferenceDocumentation", AsciidoctorTask ) {
	description = "Renders the Reference Documentation in HTML format using Asciidoctor."
	notCompatibleWithConfigurationCache( "AsciidoctorGradlePlugin does not support configuration cache yet" )

	// use of provider to obtain info and cache the value
	def versionFamily = providers.provider { project.projectVersion.family }
	def fullVersion = providers.provider { project.version.toString() }
	def asciidocReference = layout.projectDirectory.dir("src/main/asciidoc/reference")
	def unpackedThemeAsciidoc = rootProject.layout.buildDirectory.dir("unpacked-theme/hibernate-asciidoctor-theme/asciidoc")
	def docInfoHibernate = rootProject.layout.buildDirectory.dir("unpacked-theme/hibernate-asciidoctor-theme/asciidoc/docinfo/hibernate")
	def ormMinorVersion = libs.versions.hibernateOrmVersion.map { v ->
		def matcher = v =~ /\d+\.\d+/
		if (matcher.find()) {
			return matcher.group()
		} else {
			throw new IllegalArgumentException("Cannot parse the version of Hibernate ORM from '" + v + "' string.")
		}
	}

	asciidoctorj {
		version = "3.0.0"
		docExtensions( project( ":local-build-asciidoctor-extensions" ) )
	}

	sourceDir( asciidocReference )
	sources {
		include( "index.adoc" )
	}

	resources {
		from( asciidocReference ) {
			include( "images/**" )
		}
		from( unpackedThemeAsciidoc ) {
			include( "css/**", "images/**", "script/**" )
		}
	}

	outputDir = layout.buildDirectory.dir( "asciidoc/reference/html_single" )

	options( logDocuments: true )

	attributes(
			icons: "font",
			"source-highlighter": "rouge",
			majorMinorVersion: versionFamily.get(),
			fullVersion: fullVersion.get(),
			ormMinorVersion: ormMinorVersion.get(),
			stylesdir: "css",
			"iconfont-remote": false,
			"iconfont-name": "font-awesome/css/solid",
			docinfo: "shared,private",
			docinfodir: docInfoHibernate.get(),
			"html.meta.project-key": "reactive",
			"html.outdated-content.project-key": "reactive",
			"html-meta-description": "Hibernate Reactive, reactive API for Hibernate ORM - Reference Documentation",
			"html-meta-keywords": "hibernate, reactive, hibernate reactive, database, db, vert.x",
			"html-meta-version-family": versionFamily.get(),
	)

	// See https://docs.asciidoctor.org/gradle-plugin/latest/common-task-configuration/#choosing-an-execution-mode-for-asciidoctorj
	executionMode = ExecutionMode.JAVA_EXEC

	dependsOn( tasks.named( "unpackTheme" ) )
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
// All
// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

def assembleDocumentationTask = tasks.register( 'assembleDocumentation' ) {
	dependsOn aggregateJavadocsTask, renderReferenceDocumentationTask
	group 'Documentation'
	description 'Grouping task for performing all documentation building tasks'
}

assemble.dependsOn assembleDocumentationTask
