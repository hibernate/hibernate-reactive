apply from: publishScript

description = 'Hibernate Reactive Core'

dependencies {

    api ("org.hibernate:hibernate-core:${hibernateOrmVersion}") {
        //We don't need any of these:
        exclude group: 'org.javassist', module: 'javassist'
        exclude group: 'javax.activation', module: 'javax.activation-api'
    }

    //Logging
    implementation 'org.jboss.logging:jboss-logging:3.3.2.Final'
    compileOnly 'org.jboss.logging:jboss-logging-annotations:2.1.0.Final'
    annotationProcessor 'org.jboss.logging:jboss-logging-processor:2.1.0.Final'

    //Specific implementation details of Hibernate Reactive:
    implementation 'io.vertx:vertx-sql-client:3.9.1'

    //Mutiny is also exposed as API
    api 'io.smallrye.reactive:mutiny:0.5.0'

    // Testing
    testImplementation "org.hibernate:hibernate-testing:${hibernateOrmVersion}"
    testImplementation 'org.assertj:assertj-core:3.13.2'
    testImplementation 'io.vertx:vertx-unit:3.9.1'
    testImplementation 'io.vertx:vertx-pg-client:3.9.1'
    testImplementation 'io.vertx:vertx-mysql-client:3.9.1'
    testImplementation 'io.vertx:vertx-db2-client:3.9.1'

    // Testcontainers
    testImplementation 'org.testcontainers:postgresql:1.13.0'
    testImplementation 'org.testcontainers:mysql:1.13.0'
    testImplementation 'org.testcontainers:db2:1.13.0'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.30'
}

// Print a summary of the results of the tests (number of failures, successes and skipped)
def loggingSummary(db, result, desc) {
    if ( !desc.parent ) { // will match the outermost suite
        def output = "${db} results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def repeatLength = output.length() + 1
        println '\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength)
    }
}

// Configuration for the tests
tasks.withType(Test) {
    defaultCharacterEncoding = "UTF-8"
    testLogging {
        displayGranularity 1
        showStandardStreams = false
        showStackTraces = true
        exceptionFormat = 'full'
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    systemProperty 'docker', project.hasProperty( 'docker' ) ? 'true' : 'false'
}

// Example:
// gradle test -Pdb=MySQL
test {
    def selectedDb = project.hasProperty( 'db' )
            ? project.getProperty( 'db' )
            : 'PostgreSQL'
    afterSuite { desc, result ->
        loggingSummary( selectedDb, result, desc )
    }
    doFirst {
        systemProperty 'db', selectedDb
    }
}

// Rule to recognize calls to testDb<dbName>
// and run the tests on the selected db
// Example:
// gradle testDbMySQL testDbDB2
tasks.addRule( "Pattern testDb<id>" ) { String taskName ->
    if ( taskName.startsWith( "testDb" ) ) {
        task( type: Test, taskName ) {
            def dbName = taskName.substring( "testDb".length() )
            description = "Run tests for ${dbName}"

            afterSuite { desc, result ->
                loggingSummary( dbName, result, desc )
            }
            doFirst() {
                systemProperty 'db', dbName
            }
        }
    }
}

// The dbs we want to test when running testAll
def dbs = ['MySQL', 'PostgreSQL', 'DB2']
task testAll( dependsOn: dbs.collect( [] as HashSet ) { db -> "testDb${db}" } ) {
    description = "Run tests for ${dbs}"
}

spotless {
    //Don't fail during the check: rather than enforcing guidelines, we use this plugin to fix mistakes automatically.
    enforceCheck false
    java {
        licenseHeaderFile 'spotless.license.java'
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.compileJava.dependsOn(spotlessApply)
