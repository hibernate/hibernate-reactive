buildscript {
    repositories {
        // Example: ./gradlew build -PenableMavenLocalRepo
        if ( project.hasProperty( 'enableMavenLocalRepo' ) ) {
            // Useful for local development, it should be disabled otherwise
            mavenLocal()
        }
        mavenCentral()
    }
}

plugins {
    alias(libs.plugins.org.hibernate.orm)
}

description = 'Quarkus QE integration tests'

dependencies {
    implementation project(':hibernate-reactive-core')
    implementation(libs.org.hibernate.validator.hibernate.validator)
    runtimeOnly(libs.org.glassfish.expressly.expressly)

    // JPA metamodel generation for criteria queries (optional)
    annotationProcessor(libs.org.hibernate.orm.hibernate.jpamodelgen)

    // Testing on one database should be enough
    runtimeOnly(libs.io.vertx.vertx.pg.client)

    // Allow authentication to PostgreSQL using SCRAM:
    runtimeOnly(libs.com.ongres.scram.client)

    // logging
    runtimeOnly(libs.org.apache.logging.log4j.log4j.core)

    // Testcontainers
    testImplementation(libs.org.testcontainers.postgresql)

    // Testing
    testImplementation(libs.org.assertj.assertj.core)
    testImplementation(libs.io.vertx.vertx.junit5)
}

// Optional: enable the bytecode enhancements
//hibernate { enhancement }

// Print a summary of the results of the tests (number of failures, successes and skipped)
// This is the same as the one in hibernate-reactive-core
def loggingSummary(db, result, desc) {
    if ( !desc.parent ) { // will match the outermost suite
        def output = "${db} results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def repeatLength = output.length() + 1
        logger.lifecycle '\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength)
    }
}

// Example:
// gradle test -Pdb=MySQL
test {
    def selectedDb = project.hasProperty( 'db' )
            ? project.properties['db']
            : 'PostgreSQL'
    doFirst {
        systemProperty 'db', selectedDb
    }
    afterSuite { desc, result ->
        loggingSummary( selectedDb, result, desc )
    }
}

// Configuration for the tests
tasks.withType( Test ).configureEach {
    defaultCharacterEncoding = "UTF-8"
    useJUnitPlatform()
    testLogging {
        displayGranularity 1
        showStandardStreams = project.hasProperty('showStandardOutput')
        showStackTraces = true
        exceptionFormat = 'full'
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    systemProperty 'docker', project.hasProperty( 'docker' ) ? 'true' : 'false'
    systemProperty 'org.hibernate.reactive.common.InternalStateAssertions.ENFORCE', 'true'

    if ( project.hasProperty( 'includeTests' ) ) {
        // Example: ./gradlew testAll -PincludeTests=DefaultPortTest
        filter {
            includeTestsMatching project.properties['includeTests'] ?: '*' as String
        }
    }
}


// Rule to recognize calls to testDb<dbName>
// and run the tests on the selected db
// Example:
// gradle testDbMySQL testDbDB2
tasks.addRule( "Pattern testDb<id>" ) { String taskName ->
    if ( taskName.startsWith( "testDb" ) ) {
        tasks.register( taskName, Test ) {
            def dbName = taskName.substring( "testDb".length() )
            description = "Run tests for ${dbName}"

            // We only want to test this on Postgres
            onlyIf { dbName.toLowerCase().startsWith( 'p' ) }
            doFirst() {
                systemProperty 'db', dbName
            }
            afterSuite { desc, result ->
                loggingSummary( dbName, result, desc )
            }

        }
    }
}
