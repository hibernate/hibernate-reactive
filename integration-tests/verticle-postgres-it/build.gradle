buildscript {
    repositories {
        // Example: ./gradlew build -PenableMavenLocalRepo
        if ( project.hasProperty( 'enableMavenLocalRepo' ) ) {
            // Useful for local development, it should be disabled otherwise
            mavenLocal()
        }
        mavenCentral()
    }
}

description = 'Bytecode enhancements integration tests'

ext {
    jacksonDatabindVersion = '2.15.2'
    jbossLoggingVersion = '3.5.0.Final'
    assertjVersion = '3.26.3'
    vertxWebVersion = project.hasProperty( 'vertxWebVersion' )
            ? project.property( 'vertxWebVersion' )
            : vertxSqlClientVersion
    vertxWebClientVersion = project.hasProperty( 'vertxWebClientVersion' )
            ? project.property( 'vertxWebClientVersion' )
            : vertxSqlClientVersion
}

dependencies {
    implementation project(':hibernate-reactive-core')
    implementation "io.vertx:vertx-web:${vertxWebVersion}"
    implementation "io.vertx:vertx-web-client:${vertxWebClientVersion}"

    runtimeOnly "io.vertx:vertx-pg-client:${vertxSqlClientVersion}"
    // The Pg client requires this dependency
    runtimeOnly "com.ongres.scram:client:2.1"
    runtimeOnly "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"

    // logging
    implementation "org.jboss.logging:jboss-logging:${jbossLoggingVersion}"

    // Testcontainers
    implementation "org.testcontainers:postgresql:${testcontainersVersion}"

    // Testing
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "io.vertx:vertx-junit5:${vertxSqlClientVersion}"
}

// Configuration for the tests
tasks.withType( Test ).configureEach {
    defaultCharacterEncoding = "UTF-8"
    useJUnitPlatform()
    testLogging {
        showStandardStreams = project.hasProperty( 'showStandardOutput' )
        showStackTraces = true
        exceptionFormat = 'full'
        displayGranularity = 1
        events = ['PASSED', 'FAILED', 'SKIPPED']
    }
    systemProperty 'docker', project.hasProperty( 'docker' )
    systemProperty 'org.hibernate.reactive.common.InternalStateAssertions.ENFORCE', 'true'

    if ( project.hasProperty( 'includeTests' ) ) {
        // Example: ./gradlew testAll -PincludeTests=DefaultPortTest
        filter {
            includeTestsMatching project.getProperty( 'includeTests' ) ?: '*'
        }
    }
}

// Print a summary of the results of the tests (number of failures, successes and skipped)
// This is the same as the one in hibernate-reactive-core
def loggingSummary(db, result, desc) {
    if ( !desc.parent ) { // will match the outermost suite
        def output = "${db} results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def repeatLength = output.length() + 1
        logger.lifecycle '\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength)
    }
}

// Example:
// gradle test -Pdb=MySQL
test {
    def selectedDb = project.hasProperty( 'db' )
            ? project.properties['db']
            : 'PostgreSQL'
    doFirst {
        systemProperty 'db', selectedDb
    }
    afterSuite { desc, result ->
        loggingSummary( selectedDb, result, desc )
    }
}

// Rule to recognize calls to testDb<dbName>
// and run the tests on the selected db
// Example:
// gradle testDbMySQL testDbDB2
tasks.addRule( "Pattern testDb<id>" ) { String taskName ->
    if ( taskName.startsWith( "testDb" ) ) {
        tasks.register( taskName, Test ) {
            def dbName = taskName.substring( "testDb".length() )
            description = "Run tests for ${dbName}"

            // We only want to test this on Postgres
            onlyIf { dbName.toLowerCase().startsWith( 'p' ) }
            doFirst() {
                systemProperty 'db', dbName
            }
            afterSuite { desc, result ->
                loggingSummary( dbName, result, desc )
            }

        }
    }
}

tasks.register( "startVertx", JavaExec ) {
    description = "Starts the Vert.x Server app"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "org.hibernate.reactive.it.verticle.VertxServer"
}

