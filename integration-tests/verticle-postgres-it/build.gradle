buildscript {
    repositories {
        // Example: ./gradlew build -PenableMavenLocalRepo
        if ( project.hasProperty( 'enableMavenLocalRepo' ) ) {
            // Useful for local development, it should be disabled otherwise
            mavenLocal()
        }
        mavenCentral()
    }
}

description = 'Bytecode enhancements integration tests'

dependencies {
    implementation project(':hibernate-reactive-core')
    implementation(libs.io.vertx.vertx.web)
    implementation(libs.io.vertx.vertx.web.client)

    runtimeOnly(libs.io.vertx.vertx.pg.client)
    // The Pg client requires this dependency
    runtimeOnly(libs.com.ongres.scram.client)
    runtimeOnly(libs.com.fasterxml.jackson.core.jackson.databind)

    // logging
    implementation(libs.org.jboss.logging.jboss.logging)

    // Testcontainers
    implementation(libs.org.testcontainers.postgresql)

    // Testing
    testImplementation(libs.org.assertj.assertj.core)
    testImplementation(libs.io.vertx.vertx.junit5)
}

// Configuration for the tests
tasks.withType( Test ).configureEach {
    defaultCharacterEncoding = "UTF-8"
    useJUnitPlatform()
    testLogging {
        showStandardStreams = project.hasProperty( 'showStandardOutput' )
        showStackTraces = true
        exceptionFormat = 'full'
        displayGranularity = 1
        events = ['PASSED', 'FAILED', 'SKIPPED']
    }
    systemProperty 'docker', project.hasProperty( 'docker' )
    systemProperty 'org.hibernate.reactive.common.InternalStateAssertions.ENFORCE', 'true'

    if ( project.hasProperty( 'includeTests' ) ) {
        // Example: ./gradlew testAll -PincludeTests=DefaultPortTest
        filter {
            includeTestsMatching project.getProperty( 'includeTests' ) ?: '*'
        }
    }
}

// Print a summary of the results of the tests (number of failures, successes and skipped)
// This is the same as the one in hibernate-reactive-core
def loggingSummary(db, result, desc) {
    if ( !desc.parent ) { // will match the outermost suite
        def output = "${db} results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
        def repeatLength = output.length() + 1
        logger.lifecycle '\n' + ('-' * repeatLength) + '\n' + output + '\n' + ('-' * repeatLength)
    }
}

// Example:
// gradle test -Pdb=MySQL
test {
    def selectedDb = project.hasProperty( 'db' )
            ? project.properties['db']
            : 'PostgreSQL'
    doFirst {
        systemProperty 'db', selectedDb
    }
    afterSuite { desc, result ->
        loggingSummary( selectedDb, result, desc )
    }
}

// Rule to recognize calls to testDb<dbName>
// and run the tests on the selected db
// Example:
// gradle testDbMySQL testDbDB2
tasks.addRule( "Pattern testDb<id>" ) { String taskName ->
    if ( taskName.startsWith( "testDb" ) ) {
        tasks.register( taskName, Test ) {
            def dbName = taskName.substring( "testDb".length() )
            description = "Run tests for ${dbName}"

            // We only want to test this on Postgres
            onlyIf { dbName.toLowerCase().startsWith( 'p' ) }
            doFirst() {
                systemProperty 'db', dbName
            }
            afterSuite { desc, result ->
                loggingSummary( dbName, result, desc )
            }

        }
    }
}

tasks.register( "startVertx", JavaExec ) {
    description = "Starts the Vert.x Server app"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "org.hibernate.reactive.it.verticle.VertxServer"
}

