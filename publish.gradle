apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

// Java / publishing

java {
    // Configure the Java "software component" to include javadoc and sources jars in addition to the classes jar.
    // Ultimately, this component is what makes up the publication for this project.
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                // Basic JAR manifest attributes
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Specification-Vendor': 'Hibernate.org',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Hibernate.org',
                'Implementation-Vendor-Id': 'org.hibernate',
                'Implementation-Url': 'http://hibernate.org/reactive',
        )
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    publications {
        register( "publishedArtifacts", MavenPublication) {
            from components.java

            pom {
                name = project.mavenPomName
                description = project.description
                url = 'https://github.com/hibernate/hibernate-reactive'
                organization {
                    name = 'Hibernate.org'
                    url = 'https://hibernate.org'
                }
                licenses {
                    license {
                        name = 'Apache License Version 2.0'
                        url = 'https://opensource.org/licenses/Apache-2.0'
                        comments = 'See discussion at http://hibernate.org/community/license/ for more details.'
                        distribution = 'repo'
                    }
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/hibernate/hibernate-reactive/issues'
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com/hibernate/hibernate-reactive.git'
                    developerConnection = 'scm:git:ssh://git@github.com/hibernate/hibernate-reactive.git'
                    url = 'https://github.com/hibernate/hibernate-reactive.git'
                }
                developers {
                    developer {
                        id = 'hibernate-team'
                        name = 'The Hibernate Development Team'
                        organization = 'Hibernate.org'
                        organizationUrl = 'https://hibernate.org'
                    }
                }
            }
        }
    }
}


// Signing

var signingExtension = project.getExtensions().getByType(SigningExtension) as SigningExtension

def signPublicationsTask = tasks.register('signPublications') {
    description "Grouping task which executes all Sign tasks"
    dependsOn tasks.withType( Sign )
}

tasks.named( "publishPublishedArtifactsPublicationToSonatypeRepository" ) {
    // publishing depends on signing
    dependsOn signPublicationsTask
}

gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
    boolean wasSigningRequested = false
    boolean wasPublishingRequested = false

    graph.allTasks.each {task ->
        if ( task instanceof Sign ) {
            wasSigningRequested = true
        }
        else if ( task instanceof PublishToMavenRepository ) {
            wasPublishingRequested = true
        }
    }

    if ( wasPublishingRequested ) {
        def publishUser = resolvePublishUser()
        def publishPass = resolvePublishPass()
        if ( publishUser == null || publishPass == null ) {
            throw new RuntimeException( "Cannot perform publishing to OSSRH without credentials." )
        }
        logger.lifecycle "Publishing groupId: '" + project.group + "', version: '" + project.version + "'"
    }

    if ( wasSigningRequested || wasPublishingRequested ) {
        // signing was explicitly requested and/or we are publishing to Sonatype OSSRH
        // 		- we need the signing to happen
        signingExtension.required = true

        var signingKey = resolveSigningKey()
        var signingPassword = resolveSigningPassphrase()
        signingExtension.useInMemoryPgpKeys( signingKey, signingPassword )
        signingExtension.sign publishing.publications.publishedArtifacts
    }
    else {
        // signing was not explicitly requested and we are not publishing to OSSRH,
        // 		- disable all Sign tasks
        tasks.withType( Sign ).each { enabled = false }
    }
}

String resolvePublishUser() {
    var envVar = System.getenv().get( "ORG_GRADLE_PROJECT_sonatypeUsername" )
    if ( envVar != null ) {
        return envVar
    }

    def projectProp = projectPropOrNull( "sonatypeUsername" )
    if ( projectProp != null ) {
        return projectProp
    }

    return null
}

String resolvePublishPass() {
    var envVar = System.getenv().get( "ORG_GRADLE_PROJECT_sonatypePassword" )
    if ( envVar != null ) {
        return envVar
    }

    def projectProp = projectPropOrNull( "sonatypePassword" )
    if ( projectProp != null ) {
        return projectProp
    }

    return null
}

String projectPropOrNull(String name) {
    if ( project.hasProperty( name ) ) {
        return project.findProperty( name )
    }
    return null;
}


static String resolveSigningKey() {
    var key = System.getenv().get( "SIGNING_GPG_PRIVATE_KEY" )
    if ( key != null ) {
        return key
    }

    var keyFile = System.getenv().get( "SIGNING_GPG_PRIVATE_KEY_PATH" )
    if ( keyFile != null ) {
        return new File( keyFile ).text
    }

    throw new RuntimeException( "Cannot perform signing without GPG details." )
}

static String resolveSigningPassphrase() {
    var passphrase = System.getenv().get( "SIGNING_GPG_PASSPHRASE" )
    if ( passphrase == null ) {
        throw new RuntimeException( "Cannot perform signing without GPG details." )
    }
    return passphrase
}
