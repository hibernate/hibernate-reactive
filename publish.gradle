apply plugin: 'maven-publish'
apply plugin: 'nu.studer.credentials'
apply plugin: 'com.jfrog.bintray'

// To publish snapshots:
//  ./gradlew publish -PjbossNexusUser="<YOUR USERNAME>" -PjbossNexusPassword="<YOUR PASSWORD>"
// To release:
//  1. Update the version, changelog, etc.
//  2. Commit.
//  3. Tag the release.
//     > git tag -a -m "Release $RELEASE_VERSION" "$RELEASE_VERSION"
//  4. Push the tag to the repository.
//     > git push <REMOTE> "$RELEASE_VERSION"
//  5. Publish:
//     > ./gradlew bintrayUpload -PbintrayUser="<YOUR USERNAME>" -PbintrayKey="<YOUR API KEY>"

ext {
    // Credentials can be specified on the command-line using project properties,
    // or stored locally using the gradle-credentials-plugin.
    // See below for the name of project properties.
    // See https://github.com/etiennestuder/gradle-credentials-plugin to store credentials locally.
    if (!project.hasProperty('jbossNexusUser')) {
        jbossNexusUser = credentials.jbossNexusUser
    }
    if (!project.hasProperty('jbossNexusPassword')) {
        jbossNexusPassword = credentials.jbossNexusPassword
    }
    if (!project.hasProperty('bintrayUser')) {
        bintrayUser = credentials.bintrayUser
    }
    if (!project.hasProperty('bintrayKey')) {
        bintrayKey = credentials.bintrayKey
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

jar {
    manifest {
        attributes(
                // Basic JAR manifest attributes
                'Specification-Title': project.name,
                'Specification-Version': project.version,
                'Specification-Vendor': 'Hibernate.org',
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Hibernate.org',
                'Implementation-Vendor-Id': 'org.hibernate',
                'Implementation-Url': 'http://hibernate.org/reactive',
        )
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name 'jboss-snapshots-repository'
            url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
            credentials {
                username project.jbossNexusUser
                password project.jbossNexusPassword
            }
        }
    }
    publications {
        publishedArtifacts(MavenPublication) {
            groupId = project.group
            version = project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/hibernate/hibernate-reactive'
                organization {
                    name = 'Hibernate.org'
                    url = 'https://hibernate.org'
                }
                licenses {
                    license {
                        name = 'GNU Library General Public License v2.1 or later'
                        url = 'https://opensource.org/licenses/LGPL-2.1'
                        comments = 'See discussion at http://hibernate.org/community/license/ for more details.'
                        distribution = 'repo'
                    }
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/hibernate/hibernate-reactive/issues'
                }
                scm {
                    connection = 'scm:git:ssh://git@github.com/hibernate/hibernate-reactive.git'
                    developerConnection = 'scm:git:ssh://git@github.com/hibernate/hibernate-reactive.git'
                    url = 'https://github.com/hibernate/hibernate-reactive.git'
                }
                developers {
                    developer {
                        id = 'hibernate-team'
                        name = 'The Hibernate Development Team'
                        organization = 'Hibernate.org'
                        organizationUrl = 'https://hibernate.org'
                    }
                }
            }
        }
    }
}

bintray {
    user = project.bintrayUser
    key = project.bintrayKey

    publications = ['publishedArtifacts']

    pkg {
        userOrg = 'hibernate'
        repo = 'artifacts'
        name = 'hibernate-reactive'

        publish = true

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
            }
        }
    }
}
