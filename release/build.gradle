import java.nio.charset.StandardCharsets

plugins {
    // Used for publishing the documentation
    // https://github.com/ajoberstar/gradle-git-publish
    id 'org.ajoberstar.git-publish' version '3.0.0'
}

description = 'Release module'
// (Optional) before uploading the documentation, you can check
// the generated website under release/build/hibernate.org with:
// ./gradlew gitPublishCopy

// To publish the documentation:
//  1. Add the relevant SSH key to your SSH agent.
//  2. Execute this:
//     ./gradlew uploadDocumentation

// To tag a version and trigger a release on CI (which will include publishing to Bintray and publishing documentation):
//  ./gradlew ciRelease -PreleaseVersion=x.y.z.Final -PdevelopmentVersion=x.y.z-SNAPSHOT -PgitRemote=origin -PgitBranch=master

final File documentationDir = mkdir( "${project.buildDir}/documentation" );

/**
 * Assembles all documentation into the {buildDir}/documentation directory.
 */
task assembleDocumentation(dependsOn: [rootProject.project( 'documentation' ).tasks.assemble]) {
    description = 'Assembles all documentation into the {buildDir}/documentation directory'

    doLast {
        // copy documentation outputs into target/documentation.
        // 		* this is used in building the dist bundles
        //		* it is also used as a base to build a staged directory for documentation upload

        // Aggregated JavaDoc
        copy {
            from "${rootProject.project( 'documentation' ).buildDir}/javadocs"
            into "${documentationDir}/javadocs"
        }

        // Reference Documentation
        copy {
            from "${rootProject.project( 'documentation' ).buildDir}/asciidoc/reference"
            into "${documentationDir}/reference"
        }
    }
}
assemble.dependsOn assembleDocumentation

/**
 * Configuration to push the documentation on hibernate.org on GitHub
 */
gitPublish {
    // where to publish to (repo must exist)
    repoUri = 'git@github.com:hibernate/hibernate.org.git'
    // branch will be created if it doesn't exist
    branch = "production"
    // where the repository gets cloned
    repoDir = file("$buildDir/hibernate.org") // defaults to $buildDir/gitPublish
    // where to fetch from prior to fetching from the remote (i.e. a local repo to save time)
    // referenceRepoUri = 'file:///home/ddalto/workspace/hibernate.org'

    // what to publish, this is a standard CopySpec
    contents {
        from "${documentationDir}"
        into "reactive/documentation/${projectVersion.family}/"
    }

    // what to keep in the existing branch (include=keep)
    preserve {
        // =================================================================================
        // WARNING: Keep the include all or everything will be deleted before the copy task
        // =================================================================================
        include '**/*'
        exclude "reactive/documentation/${projectVersion.family}/"
    }

    // message used when committing changes
    commitMessage = "Hibernate Reactive documentation for ${projectVersion}"
}

gitPublishCopy.dependsOn assembleDocumentation

/**
 * Upload documentation on hibernate.org
 */
task uploadDocumentation {
    dependsOn = [gitPublishPush]
    description = "Upload documentation on hibernate.org"
}

task ciRelease {
    group = "Release"
    description = "Triggers the release on CI: creates commits to change the version (release, then development), creates a tag, pushes everything. Then CI will take over and perform the release."

    doFirst {
        if (!project.hasProperty('releaseVersion') || !project.hasProperty('developmentVersion')
                || !project.hasProperty('gitRemote') ||!project.hasProperty('gitBranch')) {
            throw new GradleException(
                    "Task 'ciRelease' requires all of the following properties to be set:"
                            + "'releaseVersion', 'developmentVersion', 'gitRemote' and 'gitBranch'."
            )
        }
    }

    doLast {
        logger.lifecycle("Checking that the working tree is clean...")
        String uncommittedFiles = executeGitCommand('status', '--porcelain')
        if ( !uncommittedFiles.isEmpty() ) {
            throw new GradleException(
                    "Cannot release because there are uncommitted or untracked files in the working tree."
                            + "\nCommit or stash your changes first."
                            + "\nUncommitted files:\n" + uncommittedFiles
            );
        }

        logger.lifecycle("Switching to branch '${project.gitBranch}'...")
        executeGitCommand('switch', project.gitBranch)

        logger.lifecycle("Checking that all commits are pushed...")
        String diffWithUpstream = executeGitCommand('diff', '@{u}')
        if ( !diffWithUpstream.isEmpty() ) {
            throw new GradleException(
                    "Cannot release because there are commits on the branch to release that haven't been pushed yet."
                            + "\nPush your commits to the branch to release first."
            );
        }

        logger.lifecycle("Adding commit to update version to '${project.releaseVersion}'...")
        project.projectVersionFile.text = "projectVersion=${project.releaseVersion}"
        executeGitCommand('add', '.')
        executeGitCommand('commit', '-m', project.releaseVersion)
        String tag = project.releaseVersion
        if ( tag.endsWith( ".Final" ) ) {
            tag = tag.replace( ".Final", "" )
        }

        logger.lifecycle("Tagging '${tag}'...")
        executeGitCommand('tag', '-a', '-m', "Release ${project.releaseVersion}", tag)

        logger.lifecycle("Adding commit to update version to '${project.developmentVersion}'...")
        project.projectVersionFile.text = "projectVersion=${project.developmentVersion}"
        executeGitCommand('add', '.')
        executeGitCommand('commit', '-m', project.developmentVersion)

        logger.lifecycle("Pushing branch and tag to remote '${project.gitRemote}'...")
        executeGitCommand('push', '--atomic', project.gitRemote, project.gitBranch, tag)

        logger.lifecycle("Done!")

        logger.lifecycle("Go to https://github.com/hibernate/hibernate-reactive/actions?query=branch%3A${tag} to check the progress of the automated release.")
    }
}

static String executeGitCommand(Object ... subcommand){
    List<Object> command = ['git']
    Collections.addAll( command, subcommand )
    def proc = command.execute()
    def code = proc.waitFor()
    def stdout = inputStreamToString( proc.getInputStream() )
    def stderr = inputStreamToString( proc.getErrorStream() )
    if ( code != 0 ) {
        throw new GradleException( "An error occurred while executing " + command + "\n\nstdout:\n" + stdout + "\n\nstderr:\n" + stderr )
    }
    return stdout
}

static String inputStreamToString(InputStream inputStream) {
    inputStream.withCloseable { ins ->
        new BufferedInputStream(ins).withCloseable { bis ->
            new ByteArrayOutputStream().withCloseable { buf ->
                int result = bis.read();
                while (result != -1) {
                    buf.write((byte) result);
                    result = bis.read();
                }
                return buf.toString(StandardCharsets.UTF_8.name());
            }
        }
    }
}
