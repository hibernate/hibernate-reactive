import java.nio.charset.StandardCharsets

plugins {
    id "hr-java-library"
}

description = 'Release module'
// (Optional) before uploading the documentation, you can check
// the generated website under release/build/hibernate.org with:
// ./gradlew gitPublishCopy

// The generated documentation are copied to the
// rootProject.layout.buildDirectory.dir("staging-deploy/documentation") by the updateDocumentationTask
// while the publishing is delegated to the https://github.com/hibernate/hibernate-release-scripts

// To tag a version and trigger a release on CI (which will include publishing to Bintray and publishing documentation):
//  ./gradlew ciRelease -PreleaseVersion=x.y.z.Final -PdevelopmentVersion=x.y.z-SNAPSHOT -PgitRemote=origin -PgitBranch=main

// The folder containing the rendered documentation
final Directory documentationDir = project(":documentation").layout.buildDirectory.get()

/**
 * Assembles all documentation into the {buildDir}/documentation directory.
 */
def assembleDocumentationTask = tasks.register( 'assembleDocumentation' ) {
    dependsOn ':documentation:assemble'
    group 'Documentation'
    description 'Render the documentation'
}
assemble.dependsOn assembleDocumentationTask

def updateDocumentationTask = tasks.register( 'updateDocumentation' ) {
    description "Update the documentation on the cloned static website"
    dependsOn assembleDocumentationTask

    // copy documentation outputs into target/documentation:
    // * this is used in building the dist bundles
    // * it is also used as a base to build a staged directory for documentation upload

    doLast {
        // Aggregated JavaDoc
		copy {
			from documentationDir.dir("javadocs")
			into rootProject.layout.buildDirectory.dir("staging-deploy/documentation/javadocs")
		}

        // Reference Documentation
		copy {
			from documentationDir.dir("asciidoc/reference/html_single")
			into rootProject.layout.buildDirectory.dir("staging-deploy/documentation/reference/html_single")
		}
    }
}

tasks.register( "releasePrepare" ) {
    description "Prepares all the artifacts and documentation for a JReleaser release."
    group "Release"

	// Render the Documentation/Javadocs and move them to the staging directory (for JReleaser):
	dependsOn updateDocumentationTask
	// Create all the published artifacts (i.e. jars) and move them to the staging directory (for JReleaser):
	// this one is defined in the publish.gradle
	// dependsOn project.getTasksByName(publishAllPublicationsToStagingRepository, false)
}
